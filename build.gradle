plugins {
    id 'java-library'
    id 'maven-publish'
}

def snapshot = Boolean.getBoolean("apartium.snapshot")
def isCi = System.getenv("GITHUB_ACTOR") != null

group 'net.apartium.cocoa-beans'
version '0.0.23' + (snapshot ? "-SNAPSHOT" : "")


allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            if (isCi) {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/poweredbyapartium/cocoa-beans"
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            }
            if (isCi || project.findProperty("apartium.nexus.username") != null) {
                maven {
                    name = "ApartiumMaven"
                    url = "https://nexus.voigon.dev/repository/apartium-releases"
                    credentials {
                        username = System.getenv("APARTIUM_NEXUS_USERNAME") ?: project.findProperty("apartium.nexus.username")
                        password = System.getenv("APARTIUM_NEXUS_PASSWORD") ?: project.findProperty("apartium.nexus.password")
                    }
                }
            }

        }

    }

    repositories {
        maven {
            name = "ApartiumNexus"
            url = "https://nexus.voigon.dev/repository/apartium"
        }
    }

    dependencies {
        compileOnlyApi 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
        compileOnly 'org.jetbrains:annotations:23.0.0'

    }

    test {
        useJUnitPlatform()
    }

    task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        archiveClassifier = 'javadoc'
    }

}
