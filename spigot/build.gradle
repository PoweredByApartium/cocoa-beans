plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'net.apartium.cocoa-beans'
version = parent.version

repositories {
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net'
    }

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

}

publishing {
    publications {
        spigot(MavenPublication) {
            groupId = 'net.apartium.cocoa-beans'
            artifactId = 'spigot'

            artifact packageJavadoc
            artifact jar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

}

dependencies {
    shadow project(':spigot:spigot-1-8')
    shadow project(':spigot:spigot-1-20')
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly 'com.mojang:authlib:3.11.49'
    api project(":common")
}

shadow {

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
